plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

static isSnapshotRelease(versionString) {
    versionString.endsWith('SNAPSHOT')
}

ext {
    moduleName = 'net.jqwik.mockito'
    jqwikMockitoVersion = '1.0.0-SNAPSHOT'
    jqwikVersion = '1.9.2'
	mockitoVersion = '4.11.0'
    isSnapshotRelease = isSnapshotRelease(jqwikMockitoVersion)
}


group = moduleName
version = jqwikMockitoVersion
description = "Jqwik Mockito support module"

repositories {
    mavenCentral()
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    api("net.jqwik:jqwik:${jqwikVersion}")

	// To allow people to use their own version of Mockito
    compileOnly("org.mockito:mockito-core:${mockitoVersion}")


	testImplementation("org.mockito:mockito-core:${mockitoVersion}")
}

jar {
    archiveBaseName.set('jqwik-mockito')
    archiveVersion.set("${jqwikMockitoVersion}")
    manifest {
        attributes('Automatic-Module-Name': "${moduleName}")
    }
}

java {
    withJavadocJar()
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileTestJava {
    options.compilerArgs += '-parameters'
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform {
        includeEngines 'jqwik'
    }
}

publishing {
    repositories {
        maven {
            // hint: password is in ~/.gradle/gradle.properties
            def ossrhUsername = project.hasProperty('ossrhUsername') ? project.ossrhUsername : ''
            def ossrhPassword = project.hasProperty('ossrhPassword') ? project.ossrhPassword : ''

            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }

            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = isSnapshotRelease ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
    publications {
        jqwikMockito(MavenPublication) {
            groupId = 'net.jqwik'
            artifactId = 'jqwik-mockito'
            from components.java
            pom {
                groupId = 'net.jqwik'
                name = 'jqwik-mockito'
                description = "Jqwik Mockito support module"
                url = 'https://github.com/jqwik-team/jqwik-mockito'
                licenses {
                    license {
                        name = 'Eclipse Public License - v 2.0'
                        url = 'http://www.eclipse.org/legal/epl-v20.html'
                    }
                }
                developers {
                    developer {
                        id = 'agustafson-atl'
						name = "Andrew Gustafson"
						email = "agustafson@atlassian.com"                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/jqwik-team/jqwik-mockito.git'
                    developerConnection = 'scm:git:git://github.com/jqwik-team/jqwik-mockito.git'
                    url = 'https://github.com/jqwik-team/jqwik-mockito'
                }
            }
        }
    }
}

signing {
    if (!isSnapshotRelease) {
        sign publishing.publications.jqwikMockito
    }
}

wrapper {
    gradleVersion = "8.11.1" // upgrade with: ./gradlew wrapper
}
